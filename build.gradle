plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'eclipse'
    id 'idea'

    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.3'

    id 'net.minecrell.licenser' version '0.1.2'
}

group = 'net.minecrell'
version = '0.1.3-SNAPSHOT'
description = 'A simple license header manager for Gradle'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

dependencies {
    compile localGroovy()
}

license {
    include '**/*.java'
    include '**/*.groovy'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

pluginBundle {
    website = 'https://github.com/Minecrell/licenser'
    vcsUrl = website
    description = project.description
    tags = ['license']

    plugins {
        licenserPlugin {
            id = 'net.minecrell.licenser'
            displayName = 'licenser'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact groovydocJar

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
