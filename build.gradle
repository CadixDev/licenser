plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.10'

    id 'net.minecrell.licenser' version '0.4.1'
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

group = 'org.cadixdev.gradle'
version = '0.5.0-SNAPSHOT'
description = 'A simple license header manager for Gradle'

dependencies {
    compile localGroovy()
}

license {
    include '**/*.java'
    include '**/*.groovy'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

gradlePlugin {
    plugins {
        licenser {
            id = 'org.cadixdev.licenser'
            implementationClass = 'org.cadixdev.gradle.licenser.Licenser'
        }
    }
}

pluginBundle {
    website = 'https://github.com/CadixDev/licenser'
    vcsUrl = website
    description = project.description
    tags = ['license']

    plugins {
        licenserPlugin {
            id = 'org.cadixdev.licenser'
            displayName = 'licenser'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact groovydocJar

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                }
            }
        }
    }
}
